terraform {
    backend "s3"{
        bucket = "terraform-state-bucket-aiglesias16"
        key = "global/s3/terraform.tfstate"
        region = "us-east-2"
        dynamodb_table = "terraform-state-lock-table"
        encrypt = true
    }
}


provider "aws" {
    region = "us-east-2"
}

resource "aws_s3_bucket" "tf_state_bucket" {
    bucket = "terraform-state-bucket-aiglesias16"
    
    ## Prevent the bucket from being destroyed accidentally
    lifecycle {
        prevent_destroy = true
    }
}

resource "aws_s3_bucket_versioning" "tf_state_bucket_versioning" {
    bucket = aws_s3_bucket.tf_state_bucket.id
    
    versioning_configuration {
        status = "Enabled"
    }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "tf_state_bucket_encryption" {
    bucket = aws_s3_bucket.tf_state_bucket.id

    rule {
        apply_server_side_encryption_by_default {
            sse_algorithm = "AES256"
        }
    }
}

resource "aws_s3_bucket_public_access_block" "tf_state_bucket_public_access" {
    bucket = aws_s3_bucket.tf_state_bucket.id

    block_public_acls       = true
    block_public_policy     = true
    ignore_public_acls      = true
    restrict_public_buckets = true
}

resource "aws_dynamodb_table" "tf_state_lock_table" {
    name         = "terraform-state-lock-table"
    billing_mode = "PAY_PER_REQUEST"
    hash_key     = "LockID"

    attribute {
        name = "LockID"
        type = "S"
    }
}